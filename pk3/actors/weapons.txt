ACTOR PP7Clip : Ammo
{
	Inventory.MaxAmount 7 // size of the clip
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 7 //MUST be the same as Inventory.MaxAmmount
	Inventory.Icon ICONBULL
	+INVENTORY.IGNORESKILL
}

ACTOR PP7Accuracy : Inventory
{
	Inventory.Amount 1
	Inventory.MaxAmount 8
	-INVBAR
}

ACTOR PP7 : DoomWeapon Replaces Pistol
{
	Decal BulletChip
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoGive1 7
	Weapon.AmmoGive2 14
	Weapon.AmmoType "PP7Clip"
	Weapon.AmmoType2 "9mmBullets"
	Weapon.UpSound "misc/w_pkup"
	Weapon.SisterWeapon "PP7Paintball"
	Obituary "%o was capped by %k's PP7."
	+WEAPON.WIMPY_WEAPON
	//+WEAPON.ALLOW_WITH_RESPAWN_INVUL
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOAUTOFIRE
	Inventory.Pickupmessage "PP7"
	Radius 20
	Height 16
	Attacksound "weapons/pistol"
	States
	{
		Ready:
			PP7G A 0 A_WeaponReady
			goto ReloadCheck
			PP7G AA 1 A_WeaponReady
			PP7G A 0 A_TakeInventory("PP7Accuracy", 1)
			goto Ready+1

		ReloadCheck:
		 PP7G A 0 A_JumpIfInventory("PP7Clip",7,2)
		 PP7G A 0 A_JumpIfInventory("9mmBullets",1,"ReloadStart")
		 PP7G A 0
		 goto Ready+1

		ReloadStart:
		 PP7R A 0 A_PlayWeaponSound("WeaponReload")
		 PP7R A 0 A_TakeInventory("PP7Accuracy", 2)
		 PP7R ABCDEFG 1
		 TNT1 A 5
		 goto ReloadAmmo

		ReloadAmmo:
		 TNT1 A 0 A_TakeInventory("9mmBullets",1)
		 TNT1 A 0 A_GiveInventory("PP7Clip",1)
		 TNT1 A 0 A_JumpIfInventory("PP7Clip",7,"ReloadDone")
		 TNT1 A 0 A_JumpIfInventory("9mmBullets",1,"ReloadAmmo")
		 goto ReloadDone

		ReloadDone:
		 TNT1 A 5
		 PP7R GFEDCBA 1
		 goto Ready+1

		Deselect:
			PP7G A 1 A_Lower
			TNT1 A 0 A_Lower
			Loop
		Select:
			PP7G A 1 A_Raise
			TNT1 A 0 A_Raise
			Loop
		Fire:
			PP7G A 0 A_JumpIfNoAmmo("ReloadCheck")
			PP7G B 0 BRIGHT A_Gunflash
			PP7G B 0 A_JumpIfInventory("PP7Accuracy", 8, "FireAccuracy8")
			PP7G B 0 A_JumpIfInventory("PP7Accuracy", 7, "FireAccuracy7")
			PP7G B 0 A_JumpIfInventory("PP7Accuracy", 6, "FireAccuracy6")
			PP7G B 0 A_JumpIfInventory("PP7Accuracy", 5, "FireAccuracy5")
			PP7G B 0 A_JumpIfInventory("PP7Accuracy", 4, "FireAccuracy4")
			PP7G B 0 A_JumpIfInventory("PP7Accuracy", 3, "FireAccuracy3")
			PP7G B 0 A_JumpIfInventory("PP7Accuracy", 2, "FireAccuracy2")
			PP7G B 0 A_JumpIfInventory("PP7Accuracy", 1, "FireAccuracy1")
			goto FireAccuracy0
		FireAccuracy0:
			PP7G B 1 BRIGHT A_FireBullets (0, 0, -1, 10, "BulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			PP7G B 0 A_GiveInventory("PP7Accuracy", 1)
			goto FireDone

		FireAccuracy1:
			PP7G B 1 BRIGHT A_FireBullets (1, 0.5, -1, 10, "BulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			PP7G B 0 A_GiveInventory("PP7Accuracy", 1)
			goto FireDone

		FireAccuracy2:
			PP7G B 1 BRIGHT A_FireBullets (1, 1, -1, 10, "BulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			PP7G B 0 A_GiveInventory("PP7Accuracy", 1)
			goto FireDone

		FireAccuracy3:
			PP7G B 1 BRIGHT A_FireBullets (1.5, 1, -1, 10, "BulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			PP7G B 0 A_GiveInventory("PP7Accuracy", 1)
			goto FireDone

		FireAccuracy4:
			PP7G B 1 BRIGHT A_FireBullets (1.8, 1.5, -1, 10, "BulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			PP7G B 0 A_GiveInventory("PP7Accuracy", 1)
			goto FireDone

		FireAccuracy5:
			PP7G B 1 BRIGHT A_FireBullets (2, 1.5, -1, 10, "BulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			PP7G B 0 A_GiveInventory("PP7Accuracy", 1)
			goto FireDone

		FireAccuracy6:
			PP7G B 1 BRIGHT A_FireBullets (2, 2, -1, 10, "BulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			PP7G B 0 A_GiveInventory("PP7Accuracy", 1)
			goto FireDone

		FireAccuracy7:
			PP7G B 1 BRIGHT A_FireBullets (2.5, 2, -1, 10, "BulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			PP7G B 0 A_GiveInventory("PP7Accuracy", 1)
			goto FireDone

		FireAccuracy8:
			PP7G B 1 BRIGHT A_FireBullets (3, 3, -1, 10, "BulletPuff", FBF_NORANDOM | FBF_USEAMMO)
			PP7G B 0 A_GiveInventory("PP7Accuracy", 1)
			goto FireDone

		FireDone:
			PP7G B 0 BRIGHT A_FireCustomMissile ("BulletCasing", Random(-40, -50), 0, 1, 7, 0)
			PP7G CD 1
			PP7G E 1 A_WeaponReady(WRF_NOBOB)
			PP7G F 2 A_WeaponReady(WRF_NOBOB)
			PP7G G 1 A_WeaponReady(WRF_NOBOB)
			PP7G G 0 A_TakeInventory("PP7Accuracy", 1)
			PP7G G 1 A_WeaponReady(WRF_NOBOB)
			PP7G G 0 A_TakeInventory("PP7Accuracy", 1)
			PP7G A 3 A_WeaponReady
			PP7G A 0 A_ReFire
			Goto Ready+1
		AltFire:
			TNT1 A 0
			goto ReloadCheck
		Flash:
			TNT1 A 2 Bright A_Light1
			TNT1 A 0 Bright A_Light0
			stop
		Spawn:
			PIST A -1
			Stop
	}
}

actor PP7Paintball : PP7
{
	Decal PaintBallSplat
	Weapon.SisterWeapon "PP7"
	+WEAPON.POWERED_UP
}

ACTOR AutoClip : Ammo
{
	Inventory.MaxAmount 5 // size of the clip
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 5 //MUST be the same as Inventory.MaxAmmount
	Inventory.Icon "SGNSHELL"
	+INVENTORY.IGNORESKILL
}

actor AutomaticShotgun : DoomWeapon Replaces SuperShotgun
{
  Decal BulletChip
  spawnid 33
  obituary "%o ate %k's load."
  radius 20
  height 16
  attacksound "weapons/sshotf"
  inventory.pickupmessage "Automatic Shotgun"
  Weapon.SisterWeapon "AutomaticShotgunPaintball"
  Weapon.UpSound "misc/w_pkup"
  weapon.selectionorder 400
  weapon.kickback 100
  Weapon.Ammotype "AutoClip"
  weapon.ammotype2 "ShotgunCartridges"
  weapon.ammouse 1
  weapon.ammogive 5
  weapon.ammogive2 5
  Scale 0.9
  +WEAPON.AMMO_OPTIONAL
  states
  {
  Ready:
	ASHG A 0 A_WeaponReady
	goto ReloadCheck
	ASHG A 1 A_WeaponReady
	goto Ready+1

  ReloadCheck:
	 ASHG A 0 A_JumpIfInventory("AutoClip",5,2)
	 ASHG A 0 A_JumpIfInventory("ShotgunCartridges",1,"ReloadStart")
	 ASHG A 0
	 goto Ready+1

  ReloadStart:
	 ASHG A 0 A_PlayWeaponSound("WeaponReload")
	 ASHG A 1 Offset (0, 40)
	 ASHG A 1 Offset (0, 50)
	 ASHG A 1 Offset (0, 60)
	 ASHG A 1 Offset (0, 70)
	 ASHG A 1 Offset (0, 80)
	 ASHG A 1 Offset (0, 90)
	 ASHG A 1 Offset (0, 100)
	 TNT1 A 5
	 goto ReloadAmmo

  ReloadAmmo:
	 TNT1 A 0 A_TakeInventory("ShotgunCartridges",1)
	 TNT1 A 0 A_GiveInventory("AutoClip",1)
	 TNT1 A 0 A_JumpIfInventory("AutoClip",5,"ReloadDone")
	 TNT1 A 0 A_JumpIfInventory("ShotgunCartridges",1,"ReloadAmmo")
	 goto ReloadDone

  ReloadDone:
	 TNT1 A 5
	 ASHG A 1 Offset (0, 100)
	 ASHG A 1 Offset (0, 90)
	 ASHG A 1 Offset (0, 80)
	 ASHG A 1 Offset (0, 70)
	 ASHG A 1 Offset (0, 60)
	 ASHG A 1 Offset (0, 50)
	 ASHG A 1 Offset (0, 40)
	 goto Ready+1

  Deselect:
	ASHG A 1 A_Lower
	TNT1 A 0 A_Lower
	loop
  Select:
	ASHG A 1 A_Raise
	TNT1 A 0 A_Raise
	loop
  Fire:
	ASHG A 0 A_JumpIfNoAmmo("ReloadCheck")
	ASHG A 0 A_FireCustomMissile("ShellCasing", Random(-40, -50), 0, 2, 4, 0)
	ASHG A 0 A_FireBullets (6, 5, 5, 16, "BulletPuff", FBF_NORANDOM | FBF_USEAMMO)
	ASHG A 0 A_GunFlash
	ASHG B 1
	ASHG CDEF 2
	ASHG GHI 2
	ASHG A 5
	ASHG A 2 A_ReFire
	goto Ready+1
  Altfire:
	ASHG A 0
	goto ReloadCheck
  Flash:
  	TNT1 A 0 Bright Radius_Quake(3, 2, 0, 1, 0)
  	NULL A 0 bright A_Jump(127, "Flash2")
	NULL A 2 bright A_Light2
	TNT1 A 0 bright A_Light0
	stop
  Flash2:
  	NULL B 2 bright A_Light2
  	TNT1 A 0 bright	A_Light0
  	stop
  Spawn:
	SGN2 A -1
	stop
  }
}

actor AutomaticShotgunPaintball : AutomaticShotgun
{
	Decal PaintBallSplat
	Weapon.SisterWeapon "AutomaticShotgun"
	+WEAPON.POWERED_UP
}

ACTOR ZMGClip : Ammo
{
	Inventory.MaxAmount 32 // size of the clip
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 32 //MUST be the same as Inventory.MaxAmmount
	Inventory.Icon ICONBULL
	+INVENTORY.IGNORESKILL
}

ACTOR ZMG : Weapon 5014
{
	Decal BulletChip
	Weapon.UpSound "misc/w_pkup"
	Weapon.AmmoType "ZMGClip"
	Weapon.AmmoType2 "9mmBullets"
	Weapon.AmmoGive1 32
	Weapon.AmmoGive2 40
	Weapon.AmmoUse 1
	Weapon.Selectionorder 700
	Weapon.Kickback 100
	Weapon.SisterWeapon "ZMGPaintball"
	Inventory.PickupMessage "ZMG"
	Obituary "%o was drilled by %k's ZMG"
	AttackSound "ZMGFire"
	SpawnID 214
	Radius 20
	Height 16
	+WEAPON.AMMO_OPTIONAL
	States
	{
	Spawn:
		ZMGP A -1
		Loop
  Ready:
	ZMGG A 0 A_WeaponReady
	goto ReloadCheck
	ZMGG A 1 A_WeaponReady
	goto Ready+1

  ReloadCheck:
	 ZMGG A 0 A_JumpIfInventory("ZMGClip",32,2)
	 ZMGG A 0 A_JumpIfInventory("9mmBullets",1,"ReloadStart")
	 ZMGG A 0
	 goto Ready+1

  ReloadStart:
	 ZMGR A 0 A_PlayWeaponSound("WeaponReload")
	 ZMGR ABCDEFG 1
	 TNT1 A 5
	 goto ReloadAmmo

  ReloadAmmo:
	 TNT1 A 0 A_TakeInventory("9mmBullets",1)
	 TNT1 A 0 A_GiveInventory("ZMGClip",1)
	 TNT1 A 0 A_JumpIfInventory("ZMGClip",32,"ReloadDone")
	 TNT1 A 0 A_JumpIfInventory("9mmBullets",1,"ReloadAmmo")
	 goto ReloadDone

  ReloadDone:
	 TNT1 A 5
	 ZMGR GFEDCBA 1
	 goto Ready+1
	Deselect:
		ZMGG A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		ZMGG A 1 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Fire:
		ZMGG A 0 A_JumpIfNoAmmo("ReloadCheck")
		ZMGG A 2
		ZMGG A 0 A_GunFlash
		ZMGG B 0 A_FireCustomMissile("BulletCasing", Random(-40, -50), 0, 2, 6, 0)
		ZMGG B 1 BRIGHT A_FireBullets(5, 4, 1, 12, "BulletPuff", FBF_NORANDOM | FBF_USEAMMO)
		ZMGG A 0 A_ReFire
		ZMGG CDEFGHA 2
		goto Ready+1
	AltFire:
		ZMGG A 0
		goto ReloadCheck
	Flash:
		TNT1 A 2 BRIGHT A_Light1
		TNT1 A 0 BRIGHT A_Light0
		Stop
	}
}

actor ZMGPaintball : ZMG
{
	Decal PaintBallSplat
	Weapon.SisterWeapon "ZMG"
	+WEAPON.POWERED_UP
}

ACTOR KlobbClip : Ammo
{
Inventory.MaxAmount 20 // size of the clip
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 20 //MUST be the same as Inventory.MaxAmmount
Inventory.Icon ICONBULL
+INVENTORY.IGNORESKILL
}

ACTOR Klobb : DoomWeapon Replaces Chaingun
{
	Decal BulletChip
	Weapon.UpSound "misc/w_pkup"
	Game Doom
	SpawnID 28
	Weapon.SelectionOrder 700
	Weapon.AmmoUse 1
	Weapon.AmmoGive 20
	Weapon.AmmoGive2 40
	Weapon.AmmoType "KlobbClip"
	Weapon.AmmoType2 "9mmBullets"
	Weapon.SisterWeapon "KlobbPaintball"
	Inventory.PickupMessage "Klobb"
	Obituary "%o was klobbered by %k, how humiliating."
	Attacksound "weapons/chngun"
	+WEAPON.AMMO_OPTIONAL
	States
	{
  Ready:
	KLBG A 0 A_WeaponReady
	goto ReloadCheck
	KLBG A 1 A_WeaponReady
	goto Ready+1

  ReloadCheck:
	 KLBG A 0 A_JumpIfInventory("KlobbClip",20,2)
	 KLBG A 0 A_JumpIfInventory("9mmBullets",1,"ReloadStart")
	 KLBG A 0
	 goto Ready+1

  ReloadStart:
	 KLBR A 0 A_PlayWeaponSound("WeaponReload")
	 KLBR ABCDEFG 1
	 TNT1 A 5
	 goto ReloadAmmo

  ReloadAmmo:
	 TNT1 A 0 A_TakeInventory("9mmBullets",1)
	 TNT1 A 0 A_GiveInventory("KlobbClip",1)
	 TNT1 A 0 A_JumpIfInventory("KlobbClip",20,"ReloadDone")
	 TNT1 A 0 A_JumpIfInventory("9mmBullets",1,"ReloadAmmo")
	 goto ReloadDone

  ReloadDone:
	 TNT1 A 5
	 KLBR GFEDCBA 1
	 goto Ready+1
	Deselect:
		KLBG A 1 A_Lower
		TNT1 A 0 A_Lower
		Loop
	Select:
		KLBG A 1 A_Raise
		TNT1 A 0 A_Raise
		Loop
	Hold:
		KLBG A 0 A_JumpIfNoAmmo("ReloadCheck")
		KLBG B 0 BRIGHT A_GunFlash
		KLBG B 0 A_FireCustomMissile("BulletCasing", Random(-40, -50), 0, 2, 6, 0)
		KLBG B 1 BRIGHT A_FireBullets(4, 3, 1, 4, "BulletPuff")
		KLBG A 2
		KLBG B 1 BRIGHT
		KLBG A 1
		CHGG B 0 A_ReFire
		Goto Ready+1
	Fire:
		KLBG A 0 A_JumpIfNoAmmo("ReloadCheck")
		KLBG B 0 BRIGHT A_GunFlash
		KLBG B 0 A_FireCustomMissile("BulletCasing", Random(-40, -50), 0, 2, 6, 0)
		KLBG B 1 BRIGHT A_FireBullets(2, 2, -1, 4, "BulletPuff")
		KLBG A 4
		CHGG B 0 A_ReFire
		Goto Ready+1
	AltFire:
		KLBG A 0
		goto ReloadCheck
	Flash:
		TNT1 A 2 Bright A_Light1
		TNT1 A 0 Bright A_Light0
		stop
	Spawn:
		MGUN A -1
		Stop
	}
}

actor KlobbPaintball : Klobb
{
	Decal PaintBallSplat
	Weapon.SisterWeapon "Klobb"
	+WEAPON.POWERED_UP
}

ACTOR KF7Clip : Ammo
{
Inventory.MaxAmount 30 // size of the clip
Ammo.BackpackAmount 0
Ammo.BackpackMaxAmount 30 //MUST be the same as Inventory.MaxAmmount
Inventory.Icon "POINTBUL"
+INVENTORY.IGNORESKILL
}

actor KF7Soviet : Weapon 18212
{
  Obituary "%o was riddled by %k's KF7 Soviet."
  Weapon.Ammotype1 "KF7Clip"
  Weapon.Ammotype2 "PointedBullets"
  Decal BulletChip
  Weapon.UpSound "misc/w_pkup"
  Inventory.Pickupmessage "KF7 Soviet"
  AttackSound "KF7Fire"
  Weapon.Selectionorder 650
  Weapon.AmmoUse1 1
  Weapon.AmmoGive1 30
  Weapon.AmmoGive2 30
  Weapon.SisterWeapon "KF7SovietPaintball"
  -WEAPON.NOAUTOFIRE
  +WEAPON.AMMO_OPTIONAL
  States
  {
  Ready:
	KF7S A 0 A_WeaponReady
	goto ReloadCheck
	KF7S A 1 A_WeaponReady
	goto Ready+1

  ReloadCheck:
	 KF7S A 0 A_JumpIfInventory("KF7Clip",30,2)
	 KF7S A 0 A_JumpIfInventory("PointedBullets",1,"ReloadStart")
	 KF7S A 0
	 goto Ready+1

  ReloadStart:
	 KF7R A 0 A_PlayWeaponSound("WeaponReload")
	 KF7R ABCDEFG 1
	 TNT1 A 5
	 goto ReloadAmmo

  ReloadAmmo:
	 TNT1 A 0 A_TakeInventory("PointedBullets",1)
	 TNT1 A 0 A_GiveInventory("KF7Clip",1)
	 TNT1 A 0 A_JumpIfInventory("KF7Clip",30,"ReloadDone")
	 TNT1 A 0 A_JumpIfInventory("PointedBullets",1,"ReloadAmmo")
	 goto ReloadDone

  ReloadDone:
	 TNT1 A 5
	 KF7R GFEDCBA 1
	 goto Ready+1

	Deselect:
		KF7S A 1 A_Lower
		TNT1 A 0 A_Lower
		loop
	Select:
		KF7S A 1 A_Raise
		TNT1 A 0 A_Raise
		loop
	Hold: // Intentional Fall-Through
	Fire:
		KF7S A 0 A_JumpIfNoAmmo("ReloadCheck")
		KF7S A 0 A_GunFlash
		KF7S B 1 BRIGHT A_FireBullets(4, 2, 1, 12, "BulletPuff", FBF_NORANDOM | FBF_USEAMMO)
		KF7S A 0 A_FireCustomMissile("PointedBulletCasing", Random(-40, -50), 0, 3, 6, 0)
		KF7S A 2
		KF7S C 2 BRIGHT A_FireBullets(4, 2, -1, 12, "BulletPuff", FBF_NORANDOM | FBF_USEAMMO)
		KF7S A 0 A_FireCustomMissile("PointedBulletCasing", Random(-40, -50), 0, 3, 6, 0)
		KF7S A 2
		KF7S B 1 BRIGHT A_FireBullets(4, 2, -1, 12, "BulletPuff", FBF_NORANDOM | FBF_USEAMMO)
		KF7S A 0 A_FireCustomMissile("PointedBulletCasing", Random(-40, -50), 0, 3, 6, 0)
		KF7S A 2
		KF7S D 0 A_Refire
		KF7S DEF 3
		goto Ready+1
	AltFire:
		KF7S A 0
		goto ReloadCheck
	Flash:
		TNT1 A 2 bright A_Light1
		TNT1 A 1 bright A_Light2
		TNT1 A 0 bright A_Light0
		stop
	Spawn:
		KF7P A -1
		stop
  }
}

actor KF7SovietPaintball : KF7Soviet
{
	Decal PaintBallSplat
	Weapon.SisterWeapon "KF7Soviet"
	+WEAPON.POWERED_UP
}